#AUTO
WT1000
#SETUP
SIA=1,29,14,-1<10>1
YCA=16384
MT-2.5
LCA=-15
DMA[8] 
DMPOS[4]
DMR[2]
ME1
#init
OB1,1
WT10 
OB1,0
WT100
A[0]=5
A[1]=0
A[2]=2
A[3]=0
A[4]=0
A[5]=0
A[6]=0
A[7]=0.5
POS[0]=153
POS[1]=POS[0]+4096
POS[2]=POS[1]+4096
POS[3]=POS[2]+4096
R[0]=0            
R[1]=1            
raw_pos=0
ang_err=0
calc_mov=0
setpoint=0
home=0    
ccounter=0
maxmoves=5
rres=16384
roffset=rres*8192
rstep=45.1111    
drstep=15.625    
fastsp=75000     
fastAC= 150000   
fastDC= 150000   
medsp=30000      
medAC= 50000     
medDC= 50000     
slowsp=10000     
slowAC= 10000    
slowDC= 10000    
vfastDC= 300000  
SHA
#EVENTLP
com_pos=A[0]
cur_pos=A[1]
raw_pos=_TPA - roffset
R[0] = @INT[raw_pos/rstep]
R[1] = @INT[raw_pos%rstep]
IF(com_pos<=1);setpoint=POS[0];ENDIF
IF(com_pos=2);setpoint=POS[1];ENDIF
IF(com_pos=3);setpoint=POS[2];ENDIF
IF(com_pos=4);setpoint=POS[3];ENDIF
IF(com_pos>6);setpoint=POS[3];ENDIF
ang_err=((raw_pos-setpoint)/rstep)
IF(com_pos=5);
JP#HOME
ENDIF         
IF(@ABS[ang_err]>A[7])
ccounter=0
JP#MOVE
ENDIF
A[5]=(raw_pos-home)/rstep
A[6]=ang_err             
JP#EVENTLP
EN
#MOVE
ccounter=ccounter+1
A[3]=1
bgtime=TIME
raw_pos=_TPA - roffset
raw_mov = raw_pos-setpoint
calc_mov=@INT[((raw_pos-setpoint)*drstep)]
IF(@ABS[raw_mov]>1024)
speed=A[2]            
IF(speed<=1);SPslowsp;ACslowAC;DCslowDC;ENDIF
IF(speed=2);SPmedsp;ACmedAC;DCmedDC;ENDIF
IF(speed>=3);SPfastsp;ACfastAC;DCfastDC;ENDIF
ELSE
SPslowsp;ACslowAC;DCslowDC
ENDIF
PRcalc_mov
BGA       
AMA       
raw_pos=_TPA - roffset      
ang_err = ((raw_pos-setpoint)/rstep)
IF(@ABS[ang_err]>A[7])
IF(ccounter>maxmoves);JP#HOME;ELSE;JP#MOVE;ENDIF
ENDIF
endtime=TIME
deltaT=endtime-bgtime
A[4]=deltaT
A[1]=A[0]
A[3]=0
A[5]=(raw_pos-home)/rstep
A[6]=ang_err
JP#EVENTLP
#HOME     
prev_pos=A[1]
IF(prev_pos==0);prev_pos=1;ENDIF
A[3]=1                
SPslowsp;ACslowAC;DCvfastDC
HMA                    
bgtime=TIME
BGA                    
AMA                    
OB1,1                  
WT10
OB1,0
WT100
endtime=TIME
deltaT=endtime-bgtime
A[4]=deltaT
home=_TPA - roffset  
POS[0]=home+153
POS[1]=POS[0]+4096
POS[2]=POS[1]+4096
POS[3]=POS[2]+4096
A[0]=prev_pos        
JP#EVENTLP